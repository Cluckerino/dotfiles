#!/usr/bin/env zsh
# shortcut to this dotfiles path is $DOTFILES_ROOT
export DOTFILES_ROOT=$HOME/.dotfiles

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# Stash zsh specific scripts in the ~/.zshrc.d directory. This follows the same
# logic as above, but is more specific to zsh. This allows other shells to take
# advantage of ~/.localrc.
# Files that need to be loaded first should have a prefix of "pre-" (files
# should have the form "pre-*.zsh"). Everything else gets loaded at the end.

# all of the local zsh files from the ~/.zshrc.d directory.
local_config_files=($HOME/.zshrc.d/*.zsh)

# Load local zshrc stuff appended with "pre-".
# These need to load before my scripts (especially oh-my-zsh)
for file in ${(M)local_config_files:#*/pre-*.zsh}
do
  source $file
done

# all of our zsh files in topic areas
typeset -U config_files
config_files=($DOTFILES_ROOT/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Load local zshrc stuff not appended with pre.
# These need to load after my scripts
for file in ${local_config_files:#*/pre-*.zsh}
do
  source $file
done
